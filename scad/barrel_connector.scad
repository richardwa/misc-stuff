_1_depth = 10;
_2_inner_depth = 5;
_3_outer_diameter = 6;
_4_inner_diameter = 1.8;
_5_wire_diameter = 1;
_6_wire_jut = _5_wire_diameter/2 * 0.3; 
_7_wall_thickness = 2;

$fn=64;

module barrel_connector(
  depth = 10,
  inner_depth = 5,
  outer_diameter = 4,
  inner_diameter = 2,
  wire_diameter = 1.3,
  wire_jut = 0.1,
  wall_thickness = 2) {
  $fn=32;
  difference() {
    or = outer_diameter / 2;
    ir = inner_diameter / 2;
    wr = wire_diameter / 2;
    wt = wall_thickness;
    translate([-or-wt, -or-wt, 0]) 
      cube([2 * wt + outer_diameter, 2 * wt + outer_diameter, depth + wt]);
      
    translate([-or-wr+wire_jut, 0, -.01])
      cylinder(r=wr, h=depth+wt+.02);// outer wire
    translate([ir-wr+wire_jut, 0, -.01])
      cylinder(r=wr, h=depth+wt+.02);// inner wire
    translate([0, 0, wt])
      difference() {
        cylinder(r=or, h=depth+.01); // outer barrel
        cylinder(r=ir, h=inner_depth); // inner barrel
      }
  }
}

barrel_connector(
  _1_depth,
  _2_inner_depth,
  _3_outer_diameter,
  _4_inner_diameter,
  _5_wire_diameter,
  _6_wire_jut,
  _7_wall_thickness);

